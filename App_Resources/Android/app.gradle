// Add your native dependencies here:

// Uncomment to add recyclerview-v7 dependency
//dependencies {
//	implementation 'com.android.support:recyclerview-v7:+'
//}

// If you want to add something to be applied before applying plugins' include.gradle files
// e.g. project.ext.googlePlayServicesVersion = "15.0.1"
// create a file named before-plugins.gradle in the current directory and place it there

final BUILD_DATE = new Date().format('yyyy.MM.dd-HH:mm')

def buildTypeFromEnv = System.getenv("BUILD_TYPE") ?: "debug"

def getVersionCodeTimeStamps = { ->
    def git = 'git log -1 --pretty=format:%ct'

    def proc = git.execute()
    proc.waitFor()
    def unixtime = "${proc.in.text}".trim()

    if (unixtime.isEmpty()) {
        println "Failed to get commit date"
        return -1
    }
    return (int)(Long.parseLong(unixtime) / 10)
}

def getPackageVersion = { ->
    def packageJson = new groovy.json.JsonSlurper().parseText(file('../../../package.json').text)
    return packageJson.version
}

android {
  // Determine build type: AAB (bundle) or APK (assemble)
  def buildingBundle = gradle.startParameter.taskNames.any { it.toLowerCase().contains('bundle') }

  defaultConfig {
    minSdkVersion 23
    compileSdkVersion 36
    targetSdkVersion 36
    versionCode = getVersionCodeTimeStamps()
    versionName = getPackageVersion()
    generatedDensities = []
    ndk {
      if (buildingBundle) {
        // AAB for Google Play: all architectures
        abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
      } else {
        // APK for direct distribution: arm64-v8a only
        abiFilters 'arm64-v8a'
      }
    }
  }
  aaptOptions {
    additionalParameters "--no-version-vectors"
  }

  buildTypes {
    debug {
        versionNameSuffix "-$BUILD_DATE"
        multiDexEnabled true
    }
    release {
    }
  }

  packagingOptions {
    if (!buildingBundle) {
      exclude "lib/armeabi-v7a/**"
      exclude "lib/x86/**"
      exclude "lib/x86_64/**"
    }
  }

  dependenciesInfo {
    includeInApk = false
  }

  sourceSets {
    main {
      res.srcDirs += "src/${buildTypeFromEnv}/res"
    }
    debug {
      if (buildTypeFromEnv != "debug") {
        res.srcDirs = []
      }
    }
    release {
      if (buildTypeFromEnv != "release") {
        res.srcDirs = []
      }
    }
  }
}
